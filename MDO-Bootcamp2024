Get all phish and malware mailflow...

let _startTime = datetime(2024-02-28T11:22:15Z);
let _endTime = datetime(2024-03-15T11:22:15Z);
EmailEvents
| where Timestamp between (_startTime .. _endTime)
| extend MDO_detection = parse_json(DetectionMethods)
| extend FirstDetection = iif(isempty(MDO_detection), "Clean", tostring(bag_keys(MDO_detection)[0]))
| extend FirstSubcategory = iif(FirstDetection != "Clean" and array_length(MDO_detection[FirstDetection]) > 0, strcat(FirstDetection, ": ", tostring(MDO_detection[FirstDetection][0])), "No Detection (clean)")
| where FirstSubcategory contains "Malware" or FirstSubcategory contains "Phish"
| summarize count(NetworkMessageId)

Take admin submissions, join email events to find true miss, and true miss numbers..

let _startTime = datetime(2024-02-28T11:22:15Z);
let _endTime = datetime(2024-03-15T11:22:15Z);
CloudAppEvents
| where Timestamp between (_startTime .._endTime)
| where ActionType == "AdminSubmission"
| extend SubmissionType = tostring((parse_json(RawEventData)).SubmissionType)
| extend NetworkMessageId = tostring((parse_json(RawEventData)).ObjectId)
| where SubmissionType in ("1","2")
|join kind=rightsemi (
 EmailEvents
| where ThreatTypes !contains "Phish" and ThreatTypes !contains "Malware"
| where Timestamp between (_startTime .._endTime))
on NetworkMessageId
| summarize count(NetworkMessageId)


Get all ZAP events, for phish and malware... (this includes everything, so lets filter it more, hint.. the solution's complete below ;) )

let _startTime = datetime(2024-02-16T18:10:09Z);
let _endTime = datetime(2024-03-14T18:10:09Z);
EmailPostDeliveryEvents | where ActionType in ("Malware ZAP","Phish ZAP") and Timestamp between (_startTime .._endTime )
| summarize count() by ActionResult

Get FPs we reverse-zapped..

let _startTime = datetime(2024-02-16T18:10:09Z);
let _endTime = datetime(2024-03-14T18:10:09Z);
EmailPostDeliveryEvents
| where ActionType == "Redelivery" and ThreatTypes !contains "Phish" and ThreatTypes !contains "Malware" and Timestamp between (_startTime .._endTime )
| join kind=leftsemi (
            EmailEvents
            | where Timestamp between (_startTime .._endTime )
            | where ThreatTypes contains "Phish" or ThreatTypes contains "Malware"
            | project NetworkMessageId, ThreatTypes)
            on NetworkMessageId
| summarize count(NetworkMessageId)


Lets work out the math...

Our working equation today is:

                Admin Submissions + User / Admin Triaged ZAP
________________________________________________________________________________
(Malware/Phish Mailflow) plus (Admin Submissions) plus (FN ZAPs) minus (FP ZAPs)


this means we take everything you tell us was bad, everything you removed yourself (and beat ZAP to remediating), then divide it by the total bad in the tenant, removing any FPs

MORE GOODNESS?!

Here's an example built query, you might want to abs() to fix the percentage, or even only remove the FPs which were originally PHS/MAL and now are no longer classified as that? 

//mega-big uber query for overall effectiveness numbers
let _startTime = datetime(2024-04-22T00:00:00Z);
let _endTime = datetime(2024-04-24T00:00:00Z);
let Mal_Phish_Mailflow = toscalar(
EmailEvents
| where Timestamp between (_startTime .. _endTime)
| where EmailDirection == "Inbound"
| extend MDO_detection = parse_json(DetectionMethods)
| extend FirstDetection = iif(isempty(MDO_detection), "Clean", tostring(bag_keys(MDO_detection)[0]))
| extend FirstSubcategory = iif(FirstDetection != "Clean" and array_length(MDO_detection[FirstDetection]) > 0, strcat(FirstDetection, ": ", tostring(MDO_detection[FirstDetection][0])), "No Detection (clean)")
| where FirstSubcategory contains "Malware" or FirstSubcategory contains "Phish"
| summarize count(NetworkMessageId)
);
let FP_ZAP = toscalar(
EmailPostDeliveryEvents
| where ActionType == "Redelivery" and ThreatTypes !contains "Phish" and ThreatTypes !contains "Malware" and Timestamp between (_startTime .._endTime )
| join kind=leftsemi (
            EmailEvents
            | where Timestamp between (_startTime .._endTime )
            | where ThreatTypes contains "Phish" or ThreatTypes contains "Malware"
            | project NetworkMessageId, ThreatTypes)
            on NetworkMessageId
| summarize count(NetworkMessageId)
);
let FN_ZAP_Successful = toscalar(
EmailPostDeliveryEvents
| where ActionType in ("Malware ZAP","Phish ZAP") and Timestamp between (_startTime .._endTime )and ActionResult in ("Success","AdminPolicy")
|join kind=leftsemi (
EmailEvents
| where ThreatTypes !contains "Phish" and ThreatTypes !contains "Malware"
| where Timestamp between (_startTime .._endTime))
on NetworkMessageId
| summarize count(NetworkMessageId)
);
let FN_ZAP_Unsuccessful = toscalar(
EmailPostDeliveryEvents
| where ActionType in ("Malware ZAP","Phish ZAP") and Timestamp between (_startTime .._endTime )and ActionResult !in ("Success","AdminPolicy")
|join kind=leftsemi (
EmailEvents
| where ThreatTypes !contains "Phish" and ThreatTypes !contains "Malware"
| where Timestamp between (_startTime .._endTime))
on NetworkMessageId
| summarize count(NetworkMessageId)
);
let FN_Admin_Submissions = toscalar(
CloudAppEvents
| where Timestamp between (_startTime .._endTime)
| where ActionType == "AdminSubmission"
| extend SubmissionType = tostring((parse_json(RawEventData)).SubmissionType)
| extend NetworkMessageId = tostring((parse_json(RawEventData)).ObjectId)
| where SubmissionType in ("1","2")
|join kind=rightsemi (
 EmailEvents
| where ThreatTypes !contains "Phish" and ThreatTypes !contains "Malware"
| where Timestamp between (_startTime .._endTime))
on NetworkMessageId
| summarize count(NetworkMessageId)
);
union withsource=Table
    (print StatisticName="Mal/Phish Mailflow totals - Minus FPs", Value=toreal((toscalar(Mal_Phish_Mailflow))-toreal(toscalar(FP_ZAP)))),
    (print StatisticName="Admin Mal/Phish FNs Submitted", Value=toreal(toscalar(FN_Admin_Submissions))),
    (print StatisticName="Mal/Phish FPs Reverse Zapped", Value=toreal(toscalar(FP_ZAP))),
    (print StatisticName="Mal / Phish Successfully Zapped", Value=toreal(toscalar(FN_ZAP_Successful))),
    (print StatisticName="Mal / Phish UN-Successfully Zapped", Value=toreal(toscalar(FN_ZAP_Unsuccessful))),
    (print StatisticName="Effectiveness Post Delivery", Value=abs(round(((toreal(toscalar(FN_Admin_Submissions))+toreal(toscalar(FN_ZAP_Unsuccessful)))/(toreal(toscalar(Mal_Phish_Mailflow))+toreal(toscalar(FN_ZAP_Successful))+toreal(toscalar(FN_ZAP_Unsuccessful))+toreal(toscalar(FN_Admin_Submissions))-toreal(toscalar(FP_ZAP)))*100-100),2))),
    (print StatisticName="Effectiveness Pre-Delivery", Value=abs(round(((toreal(toscalar(FN_Admin_Submissions))+toreal(toscalar(FN_ZAP_Unsuccessful))+(toscalar(FN_ZAP_Successful)))/(toreal(toscalar(Mal_Phish_Mailflow))+toreal(toscalar(FN_ZAP_Successful))+toreal(toscalar(FN_ZAP_Unsuccessful))+toreal(toscalar(FN_Admin_Submissions))-toreal(toscalar(FP_ZAP)))*100-100),2)))
| project StatisticName, Value


Want even MORE?! - lets look at Quarantine...

//Calculate the percentage of mail we quarantined vs the mail released.

let Quarantine_Releases = toscalar(EmailPostDeliveryEvents
| where Action == "Quarantine release" and Timestamp >= ago(30d)
| summarize arg_min(Timestamp,*) by NetworkMessageId,RecipientEmailAddress
| join kind=leftouter (EmailEvents | where DeliveryLocation == "Quarantine") on NetworkMessageId
| summarize count(NetworkMessageId));
let Quarantined_Mailfow = toscalar(EmailEvents | where DeliveryLocation == "Quarantine" and Timestamp >= ago(30d)
| summarize count(NetworkMessageId));
print
Quarantine_Releases = toreal(Quarantine_Releases),
Quarantined_Mailflow = toreal(Quarantined_Mailfow),
Release_Percentage = abs(round(((toreal(Quarantine_Releases)/toreal(Quarantined_Mailfow))*100),2))



Scott's Quarantine Query...

EmailPostDeliveryEvents
| where Action == "Quarantine release"
| summarize arg_min(Timestamp,*) by NetworkMessageId,RecipientEmailAddress
| join kind=leftouter (EmailEvents | where DeliveryLocation == "Quarantine") on NetworkMessageId
| summarize count() by 	SenderFromDomain,bin(Timestamp1,1d),DetectionMethods1,OrgLevelPolicy,UserLevelPolicy


